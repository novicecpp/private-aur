# original file from https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=tmux-git&id=24461c27a7de769d55a2fa740db2d62605788602

# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: C.Coutinho <kikijump[at]gmail[dot]com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: TDY <tdy@gmx.com>

pkgname=tmux-custom
_upstreamver=651891c3bd32ecc4671e8e3e36efc10d48ca0654
pkgver=master.r2.${_upstreamver:0:8}
pkgrel=1
pkgdesc="A terminal multiplexer"
url="https://github.com/tmux/tmux/wiki"
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libevent')
makedepends=('git')
depends+=('libutempter') && _addconfigureflags+="--enable-utempter "
#depends+=('libutf8proc') && _addconfigureflags+="--enable-utf8proc "
#depends+=('systemd-libs') && _addconfigureflags+="--enable-systemd "
#depends+=('libsixel') && _addconfigureflags+="--enable-sixel " # Note support by vte (gnome-terminal) yethttps://www.arewesixelyet.com/#gnome-terminal
conflicts=('tmux')
source=("git+https://github.com/tmux/tmux.git#commit=${_upstreamver}")
b2sums=('8209e78c313615782d96daa455c55bcca013578e8c38ffc6395a0a8bf475f708b8f454bd614450dcfaeff5d29e750d4321499d9102f04bdbb3982c3f50406e36')

build() {
  cd "$srcdir/tmux"
  ./autogen.sh
  ./configure --prefix=/usr ${_addconfigureflags}
  make
}

package() {
  cd "$srcdir/tmux"
  make DESTDIR="${pkgdir}" install

  install -D -m0644 COPYING "$pkgdir/usr/share/licenses/tmux/LICENSE"
}
