# original file from https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=tmux-git&id=24461c27a7de769d55a2fa740db2d62605788602

# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: C.Coutinho <kikijump[at]gmail[dot]com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: TDY <tdy@gmx.com>

pkgname=tmux-custom
#_gitname=tmux
_upstreamver=109d2bda1a7b06948e35b7e08c159e71ebc988fb
pkgver=109d2bda
pkgrel=1
pkgdesc="A terminal multiplexer"
url="https://github.com/tmux/tmux/wiki"
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libevent')
makedepends=('git')
depends+=('libutempter') && _addconfigureflags+="--enable-utempter "
#depends+=('libutf8proc') && _addconfigureflags+="--enable-utf8proc "
#depends+=('systemd-libs') && _addconfigureflags+="--enable-systemd "
#depends+=('libsixel') && _addconfigureflags+="--enable-sixel " # Note support by vte (gnome-terminal) yethttps://www.arewesixelyet.com/#gnome-terminal
conflicts=('tmux')
source=("git+https://github.com/tmux/tmux.git#commit=${_upstreamver}")
b2sums=('f1c4a14c71107e1fed9c47923b0da425b1ff33de7d35bffa8f656fff47a9a412cfcb42e8bcbe0e22251f37ba71769eb398b25d8d9b29cfd934caf2dde68cc47f')

build() {
  cd "$srcdir/tmux"
  git clean -xdf
  ./autogen.sh
  ./configure --prefix=/usr ${_addconfigureflags}
  make
}

package() {
  cd "$srcdir/tmux"
  make DESTDIR="${pkgdir}" install

  install -D -m0644 COPYING "$pkgdir/usr/share/licenses/tmux/LICENSE"
}
